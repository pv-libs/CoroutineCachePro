plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    namespace 'com.pv_libs.coroutine_cachepro'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33
        versionCode 4
        versionName "0.3.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.22"
    implementation 'androidx.core:core-ktx:1.10.1'

    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

publishing {
    publications {
        release(MavenPublication) {

            // You can then customize attributes of the publication as shown below.
            groupId = 'com.github.pv_libs'
            artifactId = 'coroutine_cachepro'
            version = '0.3.0'

            afterEvaluate {
                from components.release
            }

            // POM Configuration
            pom {
                name = 'Coroutine Cache Pro'
                description = 'Library which provides features to take full advantage of HttpCache with Retrofit'
                url = 'https://github.com/pv-libs/CoroutineCachePro'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pv-libs'
                        name = 'Pavan Varma'
                        email = 'vegesna95@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pv-libs/CoroutineCachePro.git'
                    developerConnection = 'scm:git:ssh://github.com/pv-libs/CoroutineCachePro.git'
                    url = 'https://github.com/pv-libs/CoroutineCachePro'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/pv-libs/CoroutineCachePro")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
}